buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'
// Note: only tasks and code in the "official" flavor may use Google Services.
// The "development" flavor supplies a fake google-services.json file.
apply plugin: 'com.google.gms.google-services'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" } // For CircularReveal
    google()
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    useLibrary 'org.apache.http.legacy'

    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.studio4plus.homerplayer"
        minSdkVersion 17
        targetSdkVersion rootProject.ext.targetSdkVersion
        multiDexEnabled true
        versionCode 58
        versionName getVersionName()
    }
    buildTypes {
        debug {
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'releaseType'
    productFlavors {
        development {
            firebaseCrashlytics {
                mappingFileUploadEnabled = false
            }
            dimension 'releaseType'
        }

        official {
            dimension 'releaseType'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'de.greenrobot:eventbus:2.4.1'

    implementation "androidx.appcompat:appcompat:${rootProject.ext.version_appcompat}"
    implementation 'androidx.media:media:1.2.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'com.google.guava:guava:24.1-android'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.10.8'

    implementation 'com.google.dagger:dagger:2.24'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.24'
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    implementation 'com.github.ozodrukh:CircularReveal:2.0.1@aar'

    developmentImplementation project(':app:crashreporting-noop')
    officialImplementation project(':app:crashreporting-firebase')

    developmentImplementation project(':app:analytics-noop')
    officialImplementation project(':app:analytics-firebase')
}
